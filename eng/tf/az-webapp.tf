resource "azurerm_app_service_plan" "plan" {
  name                = "plan-${var.project}"
  location            = var.location
  resource_group_name = azurerm_resource_group.rg.name

  kind     = "Linux"
  reserved = true

  sku {
    tier = "Basic"
    size = "B1"
  }
}

resource "azurerm_app_service" "api" {
  name                = "web-${var.project}"
  location            = var.location
  resource_group_name = azurerm_resource_group.rg.name
  app_service_plan_id = azurerm_app_service_plan.plan.id

  identity {
    type = "SystemAssigned"
  }

  site_config {
    dotnet_framework_version = "v5.0"
    linux_fx_version         = "DOTNETCORE|5.0"
  }

  app_settings = {
    "APPINSIGHTS_INSTRUMENTATIONKEY" = azurerm_application_insights.ai.instrumentation_key
  }
}

resource "azurerm_role_assignment" "rbac_api_cosmos" {
  scope                = azurerm_cosmosdb_account.cosmos_account.id
  role_definition_name = "Cosmos DB Account Reader Role"
  principal_id         = azurerm_app_service.api.identity.0.principal_id
}
